apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.nesscurie'
android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xbootclasspath/p:libsApi28/framework.jar"
        }
    }
}
dependencies {
    implementation files('libs/javax.obex.jar')
    implementation files('libs/com.android.vcard.jar')
    compileOnly fileTree(include: ['*.jar'], dir: '../libsApi28')
    api project(':SettingsLibApi28')
    implementation "com.github.NessCurie:RecyclerViewUtils:$recyclerViewUtilsVersion"
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation 'com.google.code.gson:gson:2.8.6'
}
preBuild {
    doLast {
        try {
            def imlFile = file("../.idea/modules/${project.name}/" + project.name + ".iml")
            println('Change ' + project.name + '.iml order')
            def parsedXml = (new XmlParser()).parse(imlFile)
            def jdkNode = parsedXml.component[1].orderEntry.find { it.'@type' == 'jdk' }
            parsedXml.component[1].remove(jdkNode)
            def sdkString = "Android API " + android.compileSdkVersion.substring("android-".length()) + " Platform"
            new groovy.util.Node(parsedXml.component[1], 'orderEntry', ['type': 'jdk', 'jdkName': sdkString, 'jdkType': 'Android SDK'])
            groovy.xml.XmlUtil.serialize(parsedXml, new FileOutputStream(imlFile))
        } catch (FileNotFoundException e) {
            // nop, iml not found
        }
    }
}
// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}